apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'

def VUFORIA_SDK_DIR = '../'

group = rootProject.ext.group

ext {
  bintrayRepo = 'maven'
  bintrayName = 'com.gigigo:irfragment'

  publishedGroupId = 'com.gigigo'
  libraryName = 'irfragment'
  artifact = 'irfragment'

  libraryDescription = 'irfragment'

  siteUrl = 'https://github.com/Gigigo-Android-Devs/gigigo-imageRecognition-library-android'
  gitUrl = 'https://github.com/Gigigo-Android-Devs/gigigo-imageRecognition-library-android.git'

  libraryVersion = rootProject.ext.version

  developerId = 'gigigo-desarrollo'
  developerName = 'Android Gigigo'
  developerEmail = 'gigigo.desarrollo@gmail.com'

  licenseName = 'The Apache Software License, Version 2.0'
  licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
  allLicenses = ["Apache-2.0"]
}

android {
  compileSdkVersion 28

  sourceSets.main {
    jni.srcDirs = []
    jniLibs.srcDir "$VUFORIA_SDK_DIR/vuforia/lib/"
  }

  defaultConfig {
    minSdkVersion 19
    targetSdkVersion 28
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    ndk {
      // Default ABI list for this app, can be over-ridden by providing an abiList property
      // e.g. gradle -PabiList=x86 clean assembleDebug
      abiFilters((project.findProperty('abiList') ?: 'armeabi-v7a, arm64-v8a, x86').split(/,\s*/))
    }
  }

  archivesBaseName = rootProject.projectDir.getName()

  // Ensure that Vuforia datasets placed in the assets folder do not get compressed as
  // this can significantly increase their loading time
  aaptOptions {
    noCompress 'dat'
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation 'com.android.support:appcompat-v7:28.0.0'
  implementation 'com.android.support:support-v4:28.0.0'
  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test:runner:1.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

  implementation files("$VUFORIA_SDK_DIR/vuforia/java/Vuforia/Vuforia.jar")
  // ARCore library
  implementation 'com.google.ar:core:1.4.0'
}

repositories {
  mavenCentral()
}

apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'